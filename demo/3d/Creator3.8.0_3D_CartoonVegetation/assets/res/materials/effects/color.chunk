uniform Color {
    vec4 hueColor;
    float ambientOcclusion;
    float randomDarken;

    float translucency;
};

//Single channel overlay
float BlendOverlay(float a, float b)
{
	return (b < 0.5) ? 2.0 * a * b : 1.0 - 2.0 * (1.0 - a) * (1.0 - b);
}

//RGB overlay
vec3 BlendOverlay(vec3 a, vec3 b)
{
	vec3 color;
	color.r = BlendOverlay(a.r, b.r);
	color.g = BlendOverlay(a.g, b.g);
	color.b = BlendOverlay(a.b, b.b);
	return color;
}


void Translucency(vec3 viewDirectionWS, float amount, float shadowAttenuation, inout vec3 color) {
  vec3 ld = normalize(cc_mainLitDir.xyz);
  float VdotL = max(0., dot(viewDirectionWS, ld)) * amount;

  //TODO: Incorperate size parameter
  VdotL = pow(VdotL, 4.) * 8.;

  //Translucency masked by shadows and grass mesh bottom
  float tMask = VdotL * shadowAttenuation;// * light.distanceAttenuation;

  //Fade the effect out as the sun approaches the horizon (75 to 90 degrees)
  // float sunAngle = dot(vec3(0., 1., 0.), ld);
  // float angleMask = clamp(sunAngle * 6.666, 0., 1.); /* 1.0/0.15 = 6.666 */

  // tMask *= angleMask;

  vec3 tColor = color + BlendOverlay(cc_mainLitColor.rgb * cc_mainLitColor.w, color);

  color = mix(color, tColor, tMask);
}


//Shading (RGB=hue - A=brightness)
vec4 applyVertexColor(in vec3 vertexPos, in vec3 wPos, in vec3 baseColor, in float mask, in float aoAmount, in float darkening, in vec4 hue, in float posOffset)
{
	vec4 col = vec4(baseColor, 1.);

	//Apply hue
	col.rgb = mix(col.rgb, hue.rgb, posOffset * hue.a);
	//Apply darkening
	float rand = fract(vertexPos.r * 4.);

	float vertexDarkening = mix(col.a, col.a * rand, darkening * mask); //Only apply to top vertices
	//Apply ambient occlusion
	float ambientOcclusion = mix(col.a, col.a * mask, aoAmount);

	col.rgb *= vertexDarkening * ambientOcclusion;

	//Pass vertex color alpha-channel to fragment stage. Used in some shading functions such as translucency
	col.a = mask;

	return col;
}